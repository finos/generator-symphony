plugins {
  id 'java-library'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots"
    mavenContent {
      snapshotsOnly()
    }
  }
}

configurations {
  botJar
}

dependencies {
  // this will be provided by the workflow-bot application at runtime
  compileOnly 'org.finos.symphony.wdk:workflow-language:<%= wdkVersion -%>'

  implementation 'org.apache.commons:commons-text:1.9'

  // used to download the standalone WDK bot jar
  botJar 'org.finos.symphony.wdk:workflow-bot-app:<%= wdkVersion -%>:boot'
}

tasks.register('botJar', Copy) {
  description "Downloads WDK jar."
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  from configurations.botJar
  into project.projectDir
  rename { f -> "workflow-bot-app.jar" }
}

tasks.register('customActivityLibs', Copy) {
  description "Copies custom activity dependencies in lib folder."
  from jar
  from configurations.runtimeClasspath
  into "lib"
}

tasks.build.dependsOn tasks.customActivityLibs
